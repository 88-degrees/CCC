/*
 Copyright (c) 2020 Mustafa Ozhan. All rights reserved.
 */
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'androidx.navigation.safeargs.kotlin'

apply from: '../dependencies.gradle'
apply from: '../version.gradle'

android {
    compileSdkVersion project.versions.compileSdkVersion

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs = ['-Xjvm-default=compatibility']
    }

    defaultConfig {
        multiDexEnabled true
        applicationId "mustafaozhan.github.com.mycurrencies"
        minSdkVersion project.versions.minSdkVersion
        targetSdkVersion project.versions.targetSdkVersion
        versionCode project.versions.versionCode
        versionName project.versions.versionName

        android {
            javaCompileOptions {
                annotationProcessorOptions {
                    arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding = true
        dataBinding = true
    }
}

dependencies {
    implementation(
            kotlin,
            androidMaterial,
            recyclerView,
            constraintLayout,
            dagger,
            moshi,
            multiDex,
            timber,
            coroutines,
            admob,
            firebaseConfig,
            navigation,
            room.values(),
            retrofit.values(),
            lifeCycle.values()
    )

    kapt(annotations.values())

    testImplementation(
            jUnit,
            mockK,
            archTesting,
            coroutinesTest
    )

    implementation(files(mxParser))

    implementation project(':ui')

    implementation project(':data')

    implementation project(':scopemob')
    implementation project(':basemob')
    implementation project(':logmob')
}
